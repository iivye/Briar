plugins {
    id 'java'
    id 'idea'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

sourceCompatibility = 21
targetCompatibility = 21
group = 'me.iivye.plugin.briar'
version = '1.1'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io/'
    }
    maven {
        name = 'redempt'
        url = 'https://redempt.dev/'
    }
    maven {
        name = 'placeholder-api'
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url = 'https://repo.rosewooddev.io/repository/public/'
    }
    maven {
        url = 'https://repo.auxilor.io/repository/maven-public/'
    }
    maven {
        url = 'https://libraries.minecraft.net/'
    }
}

dependencies {
    // Spigot/Minecraft libs
    compileOnly 'org.spigotmc:spigot-api:1.21.5-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.21'

    // Helper libs
    implementation 'com.github.cryptomorin:XSeries:13.2.0'
    implementation 'com.github.Redempt:RedLib:6.6.1'
    implementation 'org.bstats:bstats-bukkit:3.1.0'
    implementation 'com.zaxxer:HikariCP:4.0.3'

    // External plugin libs
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7.1'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'org.black_ixx:playerpoints:3.3.2'
    compileOnly 'com.github.Auxilor:EcoBits:1.8.4'

    // Datastore libs (compileOnly as they are injected/loaded during runtime to minimize JAR space and isolate from other server/plugin depends)
    compileOnly 'org.mongodb:mongo-java-driver:3.12.14'
    compileOnly 'com.mysql:mysql-connector-j:9.1.0'
    compileOnly 'org.xerial:sqlite-jdbc:3.46.0.1'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('.yml') {
        expand props
    }
}

shadowJar {
    minimize {
        exclude(dependency("com.github.cryptomorin:XSeries:."))
    }
    archiveBaseName.set("Briar")
    archiveClassifier.set("")

    relocate 'com.cryptomorin.xseries', 'me.iivye.plugin.briar.libs.xseries'
    relocate 'redempt', 'me.iivye.plugin.briar.libs.redempt'
    relocate 'org.bstats', 'me.iivye.plugin.briar.libs.bstats'
    relocate 'com.zaxxer.hikari', 'me.iivye.plugin.briar.libs.hikaricp'
}


